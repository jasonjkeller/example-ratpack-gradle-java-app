buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.io.ratpack:ratpack-gradle:1.7.5"
        classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.2"
    }
}

if (!JavaVersion.current().java8Compatible) {
  throw new IllegalStateException("Must be built with Java 8 or higher")
}

// The “ratpack” plugin applies the “application” plugin, making it easy to create a standalone application.
// See: http://gradle.org/docs/current/userguide/application_plugin.html
apply plugin: "io.ratpack.ratpack-java"
apply plugin: "com.github.johnrengelman.shadow"

repositories {
    jcenter()
}

dependencies {
    testImplementation 'junit:junit:4.13.2'

    // This is a Guice ratpack app
    implementation ratpack.dependency("guice")
    runtimeOnly 'org.slf4j:slf4j-simple:2.0.3'
}

// The “ratpack” plugin is IDEA aware.
// It will create a run configuration in IDEA to launch your app in your IDE, with hot reloading.
apply plugin: "idea"

idea {
    project {
        //use JDK 1.8 in idea
        jdkName "1.8"
        languageLevel "1.8"
        ipr {
            withXml { provider ->
                def node = provider.asNode()
                //configure git support for the project in idea
                node.component.find { it.'@name' == 'VcsDirectoryMappings' }?.mapping[0].'@vcs' = 'Git'
            }
        }
    }
}

//some CI config
apply from: file("gradle/ci.gradle")

mainClassName = "ratpack.example.java.MyApp"
